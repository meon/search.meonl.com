#!/usr/bin/perl

=head1 NAME

gen-sitemap - generate Google sitemap.xml

=head1 SYNOPSIS

        gen-sitemap

=head1 DESCRIPTION

=cut


use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use Template;
use Template::Constants qw( :debug );
use File::stat;
use File::Find::Rule;
use List::MoreUtils 'any';
use DateTime;
use File::Slurp;

use 5.010;

my @CHANGING_PAGES = qw(
    ^(../)?news.html$
    ^(../)?faq.html$
    \.rdf$
);
my @TOP_PAGES = qw(
    ^(../)?news.html$
    ^(../)?faq.html$
    ^(../)?brands/index.html$
    \.rdf$
);
my @SKIP_PAGES = qw(
    /ajax_[^/]+\.html$
);

exit main();

sub main {
    my $help;
    my $lang = 'en';
    GetOptions(
	    'help|h'   => \$help,
    ) or pod2usage;
    pod2usage if $help;
    
    my @web_files = 
	    map { detailed_file_info($_) } (
		File::Find::Rule
		    ->file()
		    ->name( '*.html', '*.rdf' )
		    ->in('html/'),
	    )
    ;
    
    
    my $config = {
	INCLUDE_PATH  => '.',
	POST_CHOMP    => 1,
	DEBUG         => DEBUG_UNDEF,
    };
    my $vars = {
		'web_files' => \@web_files,
    };
    my $template = Template->new($config);
    $template->process(\*DATA, $vars, 'html/sitemap.xml')
	    || die $template->error();
    
    return 0;
}

sub detailed_file_info {
    my $filename = shift;
    my $lastmod  = DateTime->from_epoch(epoch => stat($filename)->mtime)->ymd;
    
    $filename = substr($filename, length('html/'));
    
    return ()
	    if (any { $filename =~ m/$_/ } @SKIP_PAGES);
    
    my $priority = (any { $filename =~ m/$_/ } @TOP_PAGES) ? 1 : 0.75;
    if ($filename =~ m{/brands/} and $filename !~ m{index.html$} ) {
		my $filecontent = read_file('html/'.$filename);
		
		# low prio for devicec not from 2009 or 2008
		given ($filecontent) {
			when (/class=["']?capValue["']?>[^<]*\s2009\b</) { $priority = 0.50 }
			when (/class=["']?capValue["']?>[^<]*\s2008\b</) { $priority = 0.25 }
			when (/class=["']?capValue["']?>[^<]*\s2007\b</) { $priority = 0.10 }
			default { $priority = 0.01 }
		}
	}
    
    return {
	    'path'       => $filename,
	    'lastmod'    => $lastmod,
	    'changefreq' => (any { $filename =~ m/$_/ } @CHANGING_PAGES) ? 'weekly' : 'monthly',
	    'priority'   => $priority,
    }
}

__DATA__
<?xml version="1.0" encoding="UTF-8"?>
<urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9">
[% FOREACH web_file IN web_files %]
<url>
    <loc>http://search.meonl.com/[% web_file.path %]</loc>
    <lastmod>[% web_file.lastmod %]</lastmod>
    <changefreq>[% web_file.changefreq %]</changefreq>
    <priority>[% web_file.priority %]</priority>
</url>
[% END %]
</urlset>
